
    0  TEXT 
    5  IF ENTERED=1 THEN 10000
    6  POKE 204,0: POKE 205,11: POKE 74,0: POKE 75,11
   10  PRINT "BLOAD BAMSUB"
   15 MODE=0:PX=0:PY=0:RD=0:C=0
   16  DIM A$(15)
   17 PPL= PEEK (202):PPH= PEEK (203)
   20  DIM MI(7): FOR I=1 TO 7:MI(I)=0: NEXT I
   70 NMN=4: DIM MS(NMN),MD(NMN),MB(NMN),MA(NMN),MW(NMN),MC(NMN),MF(NMN),MT(NMN),MV(NMN)
   71  REM  MONSTER DEFINITIONS, SPEED,DEXT,BODY,ARMOR,WEAPON,COLOR,FREQ,TREAS%,TREA VALUE
   72 MS(0)=50:MD(0)=50:MB(0)=100:MA(0)=0:MW(0)=0:MC(0)=12:MF(0)=25:MT(0)=50:MV(0)=50
   74 MS(1)=100:MD(1)=100:MB(1)=50:MA(1)=1:MW(1)=0:MC(1)=15:MF(1)=30:MT(1)=85:MV(1)=100
   75  DIM MFT(4):MFT(0)=5:MFT(1)=20:MFT(2)=50:MFT(3)=20:MFT(4)=5
   76 MS(2)=150:MD(2)=150:MB(2)=50:MA(2)=2:MW(2)=1:MC(2)=9:MF(2)=35:MT(2)=75:MV(2)=200
   78 MS(3)=200:MD(3)=50:MB(3)=200:MA(3)=3:MW(3)=2:MC(3)=3:MF(3)=8:MT(3)=75:MV(3)=300
   80 MS(4)=200:MD(4)=100:MB(4)=50:MA(4)=4:MW(4)=3:MC(4)=1:MF(4)=2:MT(4)=100:MV(4)=400
  100  CALL -936: VTAB 9: TAB 13: PRINT "B E N E A T H": PRINT 
  101  TAB 9: PRINT "A P P L E   M A N O R"
  102  PRINT : TAB 13: PRINT "BY DON WORTH"
  103  VTAB 20
  104  TAB 12: PRINT "COPYRIGHT 1978"
  105  TAB 9: PRINT "THE SOFTWARE FACTORY"
  106  FOR I=1 TO 3000: NEXT I
  108  CALL -936: PRINT "INITIAL SETUP:": PRINT 
  110  PRINT : PRINT "HOW MANY ROOMS PER LEVEL (2-10)": INPUT "(I SUGGEST 5)",NR: IF NR<2 THEN 110
  111 V=20:H=4*NR: IF H<10 THEN H=10: IF H>40 THEN H=40: DIM MAP(H*V)
  112  PRINT : INPUT "BLACK & WHITE OR COLOR?",A$
  114  IF A$(1,1)="B" THEN MODE=1
  120  PRINT : PRINT "WHAT DIFFICULTY FACTOR?": PRINT 
  122  PRINT "1  - A PUSHOVER": PRINT "2  - TOO EASY": PRINT "3  - BEGINNERS ONLY"
  124  PRINT "4  - A SAFE TRIP": PRINT "5  - AVERAGE": PRINT "6  - SOME CHALLANGE": PRINT "7  - TRICKY"
  126  PRINT "8  - TOUCH AND GO": PRINT "9  - VERY DANGEROUS": PRINT "10 - YOU'RE NUTS!!!": PRINT 
  128  INPUT DF: IF DF<1 OR DF>10 THEN 128
  140 EXP=0:PSM=20:PS=20:PIM=20:PI=20:PDM=20:PD=20:PBM=20:PB=20:PA=0:PW=0
  160 BSS=20:BSI=20:BSD=20:BSB=20:BSE=0
  200  DIM RL(NR),RW(NR),RH(NR)
  220  DIM RF(NR)
  240  DIM SDL(NR)
  260  DIM MNL(NR),MNT(NR),MNA(NR),MNS(NR),MND(NR),MNB(NR)
  280  DIM CHL(NR),CHS(NR),CHC(NR)
10000  CALL -936: PRINT "CONSTRUCTING A DUNGEON LEVEL"
10002  FOR I=0 TO H*V:MAP(I)=0: NEXT I
10005 PF=(EXP+PSM*10+PIM*10+PDM*10+PBM*10)/40
10006  IF PF<20 THEN PF=20
10010  PRINT : PRINT "NOW BUILDING:": PRINT : PRINT "ROOMS"
10040  FOR I=1 TO NR
10060 RL(I)= RND (H*V): IF MAP(RL(I))#0 THEN 10060
10080 RW(I)= RND (5)+2:RH(I)= RND (3)+2
10120 X=RL(I) MOD H
10140 Y=RL(I)/H
10180  IF X+RW(I)>=H THEN 10060
10200  IF Y+RH(I)>=V THEN 10060
10220  FOR J=X TO X+RW(I)
10240  FOR K=Y TO Y+RH(I)
10260  IF MAP(K*H+J)#0 THEN 10060
10280  NEXT K,J
10285  FOR J=X TO X+RW(I)
10288  FOR K=Y TO Y+RH(I)
10290 MAP(K*H+J)=5
10292  NEXT K,J
10300  NEXT I
10320  PRINT "CORRIDORS...";
10330  FOR I=1 TO NR:RF(I)=0: NEXT I
10340  FOR I=1 TO NR-1
10360  PRINT I;" ";
10380 SX=RL(I) MOD H
10400 SY=RL(I)/H
10410 WW=RW(I)+1:HH=RH(I)+1
10420 EX=RL(I+1) MOD H
10440 EY=RL(I+1)/H
10460  IF EX-SX#WW OR EY-SY#HH THEN 10465:EX=EX+1:EY=EY+1
10465  IF  RND (2) THEN 10520
10480 DX= SGN (EX-SX):DY=0
10500  GOTO 10540
10520 DY= SGN (EY-SY):DX=0
10540  IF SX=EX AND DX#0 THEN 10800
10545  IF SY=EY AND DY#0 THEN 10800
10546  IF DX=0 AND DY=0 THEN 10800
10560 SX=SX+DX:SY=SY+DY
10570  IF MAP(SX+SY*H)=5 THEN 10540
10575 MAP(SX+SY*H)=10: IF MAP(SX-DX+(SY-DY)*H)=5 THEN MAP(SX+SY*H)=8
10580  FOR J=1 TO -1 STEP -1
10600  IF J=0 THEN 10740
10620 X=SX+J:Y=SY
10630  IF J=-DX THEN 10680
10640  IF X<0 OR X>H THEN 10680
10660  IF MAP(X+Y*H)=5 THEN 10880
10680 Y=SY+J:X=SX
10690  IF J=-DY THEN 10740
10700  IF Y<0 OR Y>V THEN 10740
10720  IF MAP(X+Y*H)=5 THEN 10880
10740  NEXT J
10780  GOTO 10540
10800  IF SX=EX AND SY=EY THEN 11080
10810 DX=0:DY=0
10820  IF SX=EX THEN DY= SGN (EY-SY)
10840  IF SY=EY THEN DX= SGN (EX-SX)
10860  GOTO 10540
10880 MAP(SX+SY*H)=8
10900  FOR J=1 TO NR
10920 SX=RL(J) MOD H
10940 SY=RL(J)/H
10945 WW=RW(J)+1:HH=RH(J)+1
10960  IF X>=SX AND X<=SX+RW(J) AND Y>=SY AND Y<=SY+RH(J) THEN 11020
10980  NEXT J
11000  PRINT "HUH?": GOTO 11000
11020  IF J=I+1 THEN 11080
11040 RF(J)=1
11060  GOTO 10420
11080  NEXT I
11090 NSD=0:PL=-1
11095  PRINT : PRINT "DOORS"
11100  FOR I=0 TO H*V
11110  IF MAP(I)#10 THEN 11120
11112  IF PL>=0 THEN 11120
11114 PL=I:PSL=I
11120  IF MAP(I)=5 THEN MAP(I)=10
11140  IF MAP(I)#8 THEN 11300
11160  IF  RND (4) THEN 11200
11180 MAP(I)=10: GOTO 11300
11200  IF  RND (2) THEN 11300
11220  IF NSD=NR THEN 11300
11240 NSD=NSD+1:SDL(NSD)=I:MAP(I)=0
11300  NEXT I: IF PL>=0 THEN 11301:PL=RL(1)+RW(1)+RH(1)*H:PSL=PL
11301  FOR I=0 TO 4:MF(I)=0: NEXT I
11302 PRT=(PF>35)+(PF>60)+(PF>120)+(PF>200)+(PF>300):J=PRT-2
11303 MFTTL=0:K=0
11304  FOR I=J TO 4: IF I<0 THEN 11308
11306  IF K<5 THEN MF(I)=MFT(K)
11307 MFTTL=MFTTL+MF(I)
11308 K=K+1: NEXT I
11325  PRINT "MONSTERS AND TREASURE"
11330 SUM=0
11340  FOR I=1 TO NR
11350 MNL(I)=-1:CHL(I)=-1
11360  IF  RND (3)=0 THEN 12000
11380 MNL(I)=RL(I)
11400 J=-1:K=0
11420 P= RND (MFTTL)
11440  FOR L=0 TO 4:K=K+MF(L): IF J>=0 THEN 11480
11460  IF P<K THEN J=L
11480  NEXT L: IF J<0 THEN J=4
11490 MAP(MNL(I))=MC(J)
11500 MNT(I)=J
11520 MNA(I)=0
11540 MNS(I)=((PF*(MS(J)/10))/50)*DF
11560 MND(I)=((PF*(MD(J)/10))/50)*DF
11580 MNB(I)=((PF*(MB(J)/10))/50)*DF
11590 SUM=SUM+MNB(I)
11600  IF  RND (100)>MT(J) THEN 12500
11620 VAL=MV(J)
11640  GOTO 12100
12000  IF  RND (5) THEN 12500
12020 VAL=0
12040 VAL=VAL+ RND (100)
12060  IF  RND (2) THEN 12040
12100 VAL=(VAL*(5+ RND (11)))/10
12110 VAL=(VAL*10)/NR
12140 CHL(I)=RL(I)+RW(I)/2+(RH(I)/2)*H
12150 MAP(CHL(I))=13
12160 CHS(I)= RND (5)
12180 CHC(I)=VAL
12185 SUM=SUM+VAL
12500  NEXT I
12510  PRINT "MAGIC ITEMS"
12520 J=0: FOR I=1 TO NR: IF CHL(I)<0 THEN 12600: IF J#0 THEN 12600: IF CHC(I)<0 THEN 12600
12540 J=1
12550  IF MNL(I)<0 OR MNT(I)#4 THEN 12560:SUM=SUM-CHC(I):CHC(I)=-99: GOTO 12600
12560 L=0: FOR K=1 TO 7: IF MI(K)#0 THEN 12580:L=K
12580  NEXT K: IF L=0 THEN 12600
12590 L= RND (7)+1: IF MI(L)#0 THEN 12590:SUM=SUM-CHC(I):CHC(I)=-L
12600  NEXT I
13000  PRINT "NOMON C"
13020  CALL -936
13030  PRINT "YOU ARE NOW A ";: GOTO 13030+PRT+1
13031  PRINT "WARRIOR": GOTO 13040
13032  PRINT "HERO": GOTO 13040
13033  PRINT "SWASHBUCKLER": GOTO 13040
13034  PRINT "CHAMPION": GOTO 13040
13035  PRINT "SUPERHERO": GOTO 13040
13036  PRINT "LORD": GOTO 13040
13037  PRINT "CHEATER": GOTO 13040
13040  PRINT : PRINT "TOTAL EXPERIENCE THIS LEVEL = ";SUM
13060  VTAB 5: PRINT "VALID COMMANDS DURING PLAY ARE:"
13080  PRINT 
13100  PRINT "N,S,E,W - MOVE NORTH,SOUTH,EAST,WEST."
13120  PRINT "          (NORTH IS UP)"
13140  PRINT "B,K     - BREAK OR KICK DOOR OPEN"
13160  PRINT "L,I     - LISTEN FOR MONSTERS OR INSPECT          FOR SECRET DOORS."
13180  PRINT "O,D     - OPEN CHEST OR DROP GOLD"
13200  PRINT "A,R     - ATTACK OR RUN AWAY."
13210  PRINT "Z,H,X,T - SPELLS: ZAP,HEAL,XRAY,TELEPORT";
13220  PRINT "1-9     - WAIT THAT MANY TURNS"
13230  PRINT "0       - WAIT UNTIL RESTED"
13240  PRINT : PRINT : INPUT "HIT RETURN TO BEGIN",A$
14000 SPH= PEEK (77)-46:SPL=169
14010  POKE 202,SPL: POKE 203,SPH: GOTO 0
    0  TEXT 
    5  IF ENTERED=1 THEN 20:ENTERED=1
   10  DIM COM$(30),RTN(30)
   16  DIM AC(3),WC(2)
   20 COM$="ABEHIKLNORSTWXZ123456789D0"
   40 RTN(1)=23000:RTN(2)=22000:RTN(3)=21000:RTN(4)=29000:RTN(5)=25000:RTN(6)=22000:RTN(7)=24000:RTN(8)=21000
   60 RTN(9)=26000:RTN(10)=27500:RTN(11)=21000:RTN(12)=29500:RTN(13)=21000:RTN(14)=30000:RTN(15)=31000
   80  FOR I=16 TO 24:RTN(I)=28000: NEXT I
   85 RTN(25)=31500:RTN(26)=28000
   90  CALL 2048:SC=0
  100 CL=0:UPN=1:UPM=1: GOTO 18000
 1000  POKE 35,23: POKE 34,20: VTAB 24
 1010  TAB 1: CALL -868
 1020  PRINT "S=";PS;" I=";PI;" D=";PD;" B=";PB;" ";GOLD;"GP EXP=";EXP;
 1040  TAB 1: VTAB 23
 1060 UPN=0: RETURN 
 2000  IF X>=0 AND X<=EXP/10 THEN 2100
 2020  INPUT "INVALID. TRY AGAIN",X: GOTO 2000
 2100 EXP=EXP-10*X: RETURN 
 3000 RD=1
 3010 PX=PL MOD H:PY=PL/H: CALL 2057
 3180 C=2: CALL 2051
 3200 UPM=0: RETURN 
 4000 J=0:DX=0:DY=0
 4020 XX=X+1:YY=Y: GOSUB 4500
 4040 XX=X-1: GOSUB 4500
 4060 XX=X:YY=Y+1: GOSUB 4500
 4080 YY=Y-1: GOSUB 4500
 4100  IF J<=1 THEN  RETURN 
 4120  INPUT "WHICH DIRECTION?",A$
 4140 XX=X:YY=Y:J=0
 4160  IF A$="N" THEN YY=Y-1
 4180  IF A$="S" THEN YY=Y+1
 4200  IF A$="E" THEN XX=X+1
 4220  IF A$="W" THEN XX=X-1
 4240  GOSUB 4500
 4260  RETURN 
 4500  IF XX<0 OR XX>=H OR YY<0 OR YY>=V THEN  RETURN 
 4502  IF CLR=255 THEN 4580
 4520  IF MAP(XX+YY*H)#CLR THEN  RETURN 
 4540 J=J+1:DX=XX-X:DY=YY-Y
 4560  RETURN 
 4580  FOR KK=0 TO NMN: IF MAP(XX+YY*H)=MC(KK) THEN 4540: NEXT KK: RETURN 
 5000  PRINT "YOU TAKE ";HITS;" HITS."
 5010 UPN=1
 5020 PB=PB-HITS: IF PB>0 THEN  RETURN 
 5040  GOSUB 6000: PRINT "YOU'RE DEAD! TOO BAD": INPUT "REINCARNATE TO LAST BRAIN SCAN?",A$
 5060  IF A$#"Y" THEN  GOTO 20540
 5080 PS=BSS:PSM=BSS:PI=BSI:PIM=BSI:PD=BSD:PDM=BSD:PB=BSB:PBM=BSB:EXP=BSE
 5090 EXP=EXP+GOLD
 5095 BSS=(BSS*9)/10:BSI=(BSI*9)/10:BSD=(BSD*9)/10:BSB=(BSB*9)/10:BSE=(BSE*9)/10
 5100  CALL -936:UPN=1:C=MAP(PL):PX=PL MOD H:PY=PL/H: CALL 2051
 5120 UPM=1:PL=PSL:G=GOLD: GOSUB 27502: RETURN 
 6000  FOR I=1 TO 500: NEXT I: RETURN 
 7000 J=0:PROB=(((D*10)/PF)*10)/(A+1)
 7020  IF W>=3 THEN PROB=PROB*2
 7025  PRINT PROB;"% ";
 7040  IF  RND (100)<PROB THEN J=1
 7060  RETURN 
10000 CL=CL+1:OUT=0
10020  IF  RND (100)=0 THEN  GOSUB 11000
10040  IF ALR THEN  GOSUB 13000
10060  IF MAF OR MRA>CL THEN  GOSUB 15000
10080  IF UPN THEN  GOSUB 1000
10100  IF UPM THEN  GOSUB 3000
10120  IF OUT=0 THEN  RETURN 
10140  POP : GOTO 18000
11000 M=0
11020  FOR I=1 TO NR: IF MNL(I)<0 THEN M=I: NEXT I
11040  IF M=0 THEN  RETURN 
11060 CLR=10:X=PL MOD H:Y=PL/H:J=0
11080 XX=X+1:YY=Y: GOSUB 4500
11100 XX=X-1: GOSUB 4500
11120 XX=X:YY=Y+1: GOSUB 4500
11140 YY=Y-1: GOSUB 4500
11160  IF J=0 THEN  RETURN 
11180 MNL(M)=X+DX+(Y+DY)*H:MAF=1
11200  PRINT "WANDERING MONSTER!"
11220 J=-1:K=0
11240 P= RND (MFTTL)
11260  FOR L=0 TO 4:K=K+MF(L): IF J>=0 THEN 11300
11280  IF P<K THEN J=L
11300  NEXT L: IF J<0 THEN J=4
11320 I=J
11360 MAP(MNL(M))=MC(I)
11380 MNT(M)=I
11400 MNA(M)=CL+5:MRA=CL+5
11420 MNS(M)=((PF*(MS(I)/10))/50)*DF
11440 MND(M)=((PF*(MD(I)/10))/50)*DF
11460 MNB(M)=((PF*(MB(I)/10))/50)*DF
11480 C=MC(I):PX=X+DX:PY=Y+DY: CALL 2051
11500  RETURN 
13000 ALR=0:X1=PL MOD H:Y1=PL/H
13020  FOR M=1 TO NR: IF MNL(M)<0 THEN 13180
13040 X2=MNL(M) MOD H:Y2=MNL(M)/H
13060  IF  ABS (X2-X1)>3 OR  ABS (Y2-Y1)>3 THEN 13180
13080 MRA=CL+5:J=CL-MNA(M)+5:MNA(M)=MRA
13085  IF MNT(M)#2 THEN 13095
13088 MNB(M)=MNB(M)+(PF/20)*J
13090 I=((PF*(MB(MNT(M))/10))/50)*DF
13091  IF MNB(M)>I THEN MNB(M)=I
13095  IF J<5 THEN 13180
13100  IF J=0 THEN 13180
13120 S=((PF*(MS(MNT(M))/10))/50)*DF:D=((PF*(MD(MNT(M))/10))/50)*DF
13130  IF MNS(M)=S THEN 13140:MNS(M)=MNS(M)+(PF/20): IF MNS(M)>S THEN MNS(M)=S:J=J-1: IF J=0 THEN 13180
13140  IF MND(M)=D THEN 13160:MND(M)=MND(M)+(PF/20): IF MND(M)>D THEN MND(M)=D:J=J-1: IF J=0 THEN 13180
13160  IF MNS(M)<S OR MND(M)<D THEN 13100
13180  NEXT M: RETURN 
15000 MAF=0:X1=PL MOD H:Y1=PL/H
15020  FOR M=1 TO NR: IF MNL(M)<0 THEN 15160
15040  IF MNA(M)<=CL THEN 15160
15060 X2=MNL(M) MOD H:Y2=MNL(M)/H
15080  IF  ABS (X2-X1)>1 OR  ABS (Y2-Y1)>1 THEN 15140
15100  IF  ABS (X2-X1)#0 AND  ABS (Y2-Y1)#0 THEN 15140
15120  GOSUB 16000: GOTO 15160
15140  GOSUB 17000
15160  NEXT M: RETURN 
16000  IF MNS(M)<(PF/20)*2 OR MND(M)=0 THEN  RETURN 
16020 ALR=1:OUT=1:UPN=1
16040 A=PA:D=MND(M):W=MW(MNT(M)): GOSUB 7000
16060 MNS(M)=MNS(M)-(PF/20): PRINT "MONSTER ATTACKS...";: GOSUB 6000
16080  IF J#0 THEN 16120
16100  PRINT "AND MISSES.": RETURN 
16120 HITS=((W+1)*2*MNS(M))/20
16130  IF HITS=0 THEN HITS=1
16140  IF MNT(M)=1 THEN 16200
16160  IF MNT(M)=0 THEN 16300
16180  GOSUB 5000: RETURN 
16200 PSM=PSM-HITS:PS=PS-HITS: PRINT "YOU LOSE ";HITS;" STR."
16220  IF PSM<0 THEN  GOSUB 5040: IF PS<0 THEN PS=0: RETURN 
16300  IF PA=0 OR PA>3 THEN 16180: IF  RND (4) THEN 16180:PA=PA-1
16320  PRINT "YOUR ARMOR IS DAMAGED.": RETURN 
17000 DX= SGN (X1-X2):DY= SGN (Y1-Y2)
17020 XX=X2+DX:YY=Y2: IF MAP(XX+YY*H)=10 THEN 17065
17040 XX=X2:YY=Y2+DY: IF MAP(XX+YY*H)=10 THEN 17065
17060  RETURN 
17065 Z=PF/100: IF MND(M)<Z THEN  RETURN :MND(M)=MND(M)-Z
17080 MAP(MNL(M))=10:MNL(M)=XX+YY*H:MAP(MNL(M))=MC(MNT(M))
17120 PX=X2:PY=Y2: CALL 2054
17124  IF C#MC(MNT(M)) THEN 17140:C=10
17126  CALL 2051
17140  IF  ABS (X1-XX)>1 OR  ABS (Y1-YY)>1 THEN  RETURN :MAF=1:UPM=1
17160  IF  RND (3) THEN MNA(M)=MNA(M)+1: RETURN 
18000  IF UPN THEN  GOSUB 1000
18020  IF UPM THEN  GOSUB 3000
18060 S=-16336:J=10
18061  FOR I=1 TO J:X= PEEK (S)+ PEEK (S)+ PEEK (S)+ PEEK (S)
18100 INP= PEEK (-16384): IF INP<128 THEN 18100
18120  POKE -16368,0
18510  CALL -936
18520 R=18580: FOR I=1 TO  LEN(COM$)
18540  IF INP= ASC(COM$(I,I)) THEN R=RTN(I)
18560  NEXT I: GOTO R
18580  PRINT "WHAT? ENTER ONE OF THE FOLLOWING:"
18590  PRINT COM$
18600  GOTO 18060
20000 UPN=1:UPM=1:BANK=BANK+GOLD:GOLD=BANK:BANK=0:PS=PSM:PI=PIM:PD=PDM:PB=PBM
20001  GOSUB 1000:C=10:PX=PL MOD H:PY=PL/H: CALL 2051
20002  PRINT "ENTER OPTION: TRADE,WEAPON,ARMOR,SCAN,": INPUT "DEEPER,RETURN,END - ",A$
20003  IF  LEN(A$)<1 THEN 20002
20004  IF A$(1,1)="T" THEN  GOSUB 20020
20005  IF A$(1,1)="W" THEN  GOSUB 20340
20006  IF A$(1,1)="A" THEN  GOSUB 20230
20007  IF A$(1,1)="R" THEN 20900
20009  IF A$(1,1)="D" THEN 20520
20010  IF A$(1,1)="E" THEN 20540
20018  IF A$(1,1)="S" THEN  GOSUB 20600
20019  GOTO 20002
20020  PRINT "EXPERIENCE MAY BE CONVERTED 10 FOR 1 FORANY ATTRIBUTE."
20040  INPUT "HOW MUCH MORE STRENGTH",X
20060  GOSUB 2000:PSM=PSM+X:PS=PS+X: GOSUB 1000
20080  INPUT "INTELLIGENCE",X
20100  GOSUB 2000:PIM=PIM+X:PI=PI+X: GOSUB 1000
20120  INPUT "DEXTERITY",X
20140  GOSUB 2000:PDM=PDM+X:PD=PD+X: GOSUB 1000
20160  INPUT "BODY",X
20180  GOSUB 2000:PBM=PBM+X:PB=PB+X: GOSUB 1000
20200  RETURN 
20230 AC(1)=50:AC(2)=100:AC(3)=250
20240  PRINT "1-LEATHER (50GP), 2-CHAIN (100GP),"
20250  PRINT "3-PLATE (250GP)"
20260  PRINT "YOU HAVE ";PA;". WHICH DO YOU WANT";: INPUT X
20265  IF X<0 OR X>3 THEN 20240
20266  IF X<=(PA) THEN 20300
20268  IF AC(X)<=GOLD THEN 20270
20269  GOSUB 20450: GOTO 20260
20270 GOLD=GOLD-AC(X)
20272 PA=X
20274  PRINT "ITS YOURS..."
20300  GOSUB 1000: GOSUB 6000: RETURN 
20340 WC(1)=50:WC(2)=100
20360  PRINT "1-HAND AXE (50GP), 2-SWORD (100GP)"
20380  PRINT "YOU HAVE ";PW;". WHICH DO YOU WANT";: INPUT X
20400  IF X<0 OR X>2 THEN 20360
20420  IF X<=(PW) THEN 20500
20440  IF WC(X)<=GOLD THEN 20460
20449  GOSUB 20450: GOTO 20380
20450  PRINT "YOU HAVEN'T ENOUGH GOLD": RETURN 
20460 GOLD=GOLD-WC(X)
20480 PW=X
20490  PRINT "ITS YOURS..."
20500  GOSUB 1000: GOSUB 6000: RETURN 
20520 BANK=GOLD:GOLD=0: POKE 202,PPL: POKE 203,PPH: GOTO 0
20530  END 
20540  TEXT : CALL -936: PRINT "OK - SEE YOU LATER": END 
20600 I=(PSM+PIM+PDM+PBM)*10+EXP
20605 I=I/5
20610  PRINT "LAST SCAN: S-";BSS;" I-";BSI;" D-";BSD;" B-";BSB;" EXP-";BSE
20611  PRINT "NEW SCAN COST-";I;"GP.";: INPUT " SCAN? ",A$
20612  IF  LEN(A$)=0 THEN  RETURN : IF A$(1,1)#"Y" THEN  RETURN 
20620  IF GOLD>=I THEN 20660
20640  PRINT "YOU CAN'T AFFORD IT.": GOSUB 6000: RETURN 
20660 GOLD=GOLD-I:BSS=PSM:BSI=PIM:BSD=PDM:BSB=PBM:BSE=EXP
20680  GOSUB 1000: PRINT "YOU HAVE BEEN SCANNED": GOSUB 6000: RETURN 
20900 BANK=GOLD:GOLD=0
20920  CALL -936:UPN=1
20930  IF BANK=0 THEN 18000
20940  PRINT "I WILL PUT THE REST OF YOUR GOLD IN THE"
20960  PRINT "BANK FOR SAFE KEEPING."
20999  GOTO 18000
21000 DX=0:DY=0
21010  IF PD>0 THEN 21020
21015  PRINT "YOUR DEXTERITY IS ZERO, BETTER REST": GOTO 18060
21020  IF INP= ASC("N") THEN DY=-1
21040  IF INP= ASC("S") THEN DY=1
21060  IF INP= ASC("E") THEN DX=1
21080  IF INP= ASC("W") THEN DX=-1
21100 X=PL MOD H:Y=PL/H:XX=X+DX:YY=Y+DY
21120  IF XX<0 OR XX>=H OR YY<0 OR YY>=V THEN 21300
21140  IF MAP(XX+YY*H)#10 THEN 21300
21150 J=PS-(GOLD/10)-PA*2: IF J>=0 THEN NCL=1: IF J<0 THEN NCL=2: IF J>=-PS THEN 21154
21151  IF GOLD=0 THEN 21154
21152  PRINT "YOU ARE TOO LOADED DOWN TO MOVE": GOTO 18060
21154 NCL=(NCL*PF)/PD: IF NCL>0 THEN 21155:NCL=1
21155 SC=SC+1: IF SC<5 THEN 21160:PD=PD-(PF/20):SC=0
21160 PL=XX+YY*H:UPM=1:UPN=1
21170  IF  RND (10)=9 THEN ALR=1
21180  IF PL#PSL THEN 21260
21200  PRINT "YOU ARE AT THE MAIN STAIRCASE."
21220  INPUT "DO YOU WANT TO EXIT TO SPEND EXP/GOLD?",A$
21230  GOSUB 3000
21235  IF  LEN(A$)<1 THEN 21260
21240  IF A$(1,1)="Y" THEN 20000
21260  IF NCL=0 THEN 18000:NCL=NCL-1: GOSUB 10000: GOTO 21260
21300  GOTO 18060
22000 CLR=8:X=PL MOD H:Y=PL/H: GOSUB 4000
22020  IF J#0 THEN 22080
22040  PRINT "I SEE NO DOOR"
22060  GOTO 18060
22080 P= RND (100):ALR=1
22100  IF INP= ASC("K") THEN P=P*2
22110  IF MI(5)=1 THEN 22180
22120  IF (P/10)<=(6*PS)/PF THEN 22180
22130 PS=PS-(PF/20):UPN=1
22140  PRINT "WHAM!!! THE DOOR DOESN'T BUDGE"
22160  GOSUB 10000: GOTO 18000
22180 PS=PS-(PF/20):UPN=1
22190  PRINT "CRASH!! THE DOOR GIVES WAY"
22200 PL=X+DX+(Y+DY)*H:UPM=1
22210  IF INP# ASC("B") THEN 22160
22220 XD=DX:YD=DY: GOSUB 3000:DX=XD:DY=YD
22240  GOSUB 1000: GOTO 21100
23000 CLR=255:X=PL MOD H:Y=PL/H: GOSUB 4000
23020  IF J#0 THEN 23060
23040  PRINT "I SEE NO MONSTER": GOTO 18060
23060 X=X+DX:Y=Y+DY
23080  FOR M=1 TO NR: IF (X+Y*H)=MNL(M) THEN 23090: NEXT M: GOTO 23040
23090  IF INP# ASC("Z") THEN 23100
23095 W=3:DMG= RND (PIM/2): IF MI(7)=0 THEN PI=PI-DMG: GOTO 23101
23100  IF PS<PF/20 THEN 23480:W=PW:PS=PS-(PF/20):DMG=((W+1)*2*PS)/20
23101 UPN=1:A=MA(MNT(M)):D=PD: GOSUB 7000
23110 ALR=1
23120  IF J#0 THEN 23160
23140  PRINT "YOU MISSED": GOSUB 10000: GOTO 18000
23160  IF MNT(M)#1 THEN 23220
23180  IF INP= ASC("Z") OR PW>2 THEN 23220
23200  PRINT "ONLY MAGICAL WEAPONS AFFECT GHOSTS": GOSUB 10000: GOTO 18000
23220  IF DMG=0 THEN DMG=1:MNB(M)=MNB(M)-DMG
23280  GOTO  RND (3)*20+23300
23300  PRINT "AAARG!!!";: GOTO 23400
23320  PRINT "OUCH!!";: GOTO 23400
23340  PRINT "YOWLP!!";
23400  PRINT " YOU INFLICT ";DMG;" HITS": IF MNB(M)>0 THEN 23470
23420 MAP(MNL(M))=10:MNL(M)=-1
23440  PRINT "THE MONSTER IS DEAD"
23450 UPM=1
23460 EXP=EXP+((PF*(MB(MNT(M))/10))/50)*DF
23470  GOSUB 10000: GOTO 18000
23480  PRINT "YOU ARE TOO WEAK TO FIGHT": GOTO 18060
24000  PRINT "SHHHH..."
24010 CLR=8:X=PL MOD H:Y=PL/H: GOSUB 4000: IF J#0 THEN 24020:DX=0:DY=0
24020 J=0:X=(PL MOD H)+DX:Y=(PL/H)+DY
24040  FOR DX=1 TO -1 STEP -1
24060  FOR DY=1 TO -1 STEP -1
24080 XX=X+DX:YY=Y+DY
24090  IF  ABS (DX)= ABS (DY) THEN 24300
24100  IF XX<0 OR XX>=H OR YY<0 OR YY>=V THEN 24300
24120  FOR II=0 TO NMN: IF MAP(XX+YY*H)=MC(II) THEN 24140: NEXT II: GOTO 24300
24140  GOTO 24200+II*20
24200  PRINT "YOU HEAR OOZING": GOSUB 6000:J=J+1: GOTO 24300
24220  GOTO 24300
24240  PRINT "YOU HEAR 'TROLLISH'": GOSUB 6000:J=J+1: GOTO 24300
24260  PRINT "YOU HEAR SLITHERING": GOSUB 6000:J=J+1: GOTO 24300
24280  PRINT "YOU HEAR HISSING": GOSUB 6000:J=J+1
24300  NEXT DY,DX
24320  IF J=0 THEN  PRINT "YOU HEAR NOTHING"
24340  GOTO 18000
25000  GOSUB 10000: GOSUB 10000: GOSUB 10000:J=0
25020 X=PL MOD H:Y=PL/H
25040  FOR I=1 TO NSD
25080 XX=(SDL(I) MOD H)-X:YY=(SDL(I)/H)-Y
25100  IF (XX<-1 OR XX>1) OR (YY<-1 OR YY>1) THEN 25200
25120 MAP(SDL(I))=8:J=J+1
25200  NEXT I
25300  IF J=0 THEN  PRINT "NO SECRET DOORS"
25320  IF J#0 THEN UPM=1
25400  GOTO 18000
26000 CLR=13:X=PL MOD H:Y=PL/H: GOSUB 4000
26020  IF J#0 THEN 26080
26022 CLR=8: GOSUB 4000: IF J=0 THEN 26040: IF MI(5) THEN 22200: PRINT "ITS LOCKED": GOTO 18000
26040  PRINT "I SEE NO CHEST"
26060  GOTO 18060
26080 X=X+DX:Y=Y+DY
26100  FOR CH=1 TO NR
26120  IF (X+Y*H)=CHL(CH) THEN 26160
26140  NEXT CH: GOTO 26040
26160  IF CHS(CH)=0 THEN 26680
26180  PRINT "THE CHEST IS LOCKED"
26200  INPUT "BREAK THE LOCK?",A$
26220  IF A$#"Y" THEN 18060
26230 ALR=1
26240  GOTO 26300+(CHS(CH)-1)*100
26300  PRINT "WHEW! NO TRAPS": GOSUB 6000
26320  GOTO 26660
26400  PRINT "POISON GAS!": GOSUB 6000
26420  IF  RND (10)<(PD*5)/PF THEN 26460
26440 HITS= RND (PF/2)+1: GOSUB 5000: GOTO 26660
26460  PRINT "IT MISSED YOU": GOTO 26660
26500  PRINT "POISON DART!": GOSUB 6000
26520  IF  RND (10)<(PI*5)/PF THEN 26460
26540  GOTO 26440
26600  PRINT "ACID!": GOSUB 6000
26620  IF  RND (10)<(PD*5)/PF THEN 26460
26640  GOTO 26440
26660 CHS(CH)=0
26680  IF CHC(CH)<0 THEN 26800
26700  IF CHC(CH)>0 THEN 26740
26720  PRINT "THE CHEST IS EMPTY": GOTO 26780
26740 GOLD=GOLD+CHC(CH):EXP=EXP+CHC(CH)
26760  PRINT "THE CHEST CONTAINED ";CHC(CH);"GP"
26780 CHC(CH)=0:UPN=1: GOSUB 10000: GOTO 18000
26800  PRINT "YOU FIND ";
26810  IF CHC(CH)=-99 THEN  PRINT "A GOLDEN APPLE. ";
26820  IF CHC(CH)=-1 THEN  PRINT "A SWORD. ";
26840  IF CHC(CH)=-2 THEN  PRINT "ARMOR. ";
26860  IF CHC(CH)>-5 AND CHC(CH)<-2 THEN  PRINT "A POTION. ";
26880  IF CHC(CH)<-4 AND CHC(CH)>-8 THEN  PRINT "A WAND ";
26900  INPUT "DO YOU TAKE IT?",A$
26920  IF A$(1,1)="Y" THEN  GOTO 26960
26930  IF A$(1,1)#"N" THEN 26900
26940  PRINT "POOF! ITS GONE": GOTO 26780
26960 X=-CHC(CH): IF X=99 THEN 32500:MI(X)=1:CHC(CH)=0
26980  GOTO 27000+X*50
27050 PW=3
27060  PRINT "ITS MAGIC!": GOTO 26780
27100 PA=4: GOTO 27060
27150  PRINT "YOUR MAX STRENGTH IS DOUBLED!"
27155 PS=PS+PSM:PSM=PSM*2: GOTO 26780
27200  CALL 2048: PRINT "YOU LOST YOUR MEMORY":UPM=1: GOTO 26780
27250  PRINT "IT UNLOCKS DOORS": GOTO 26780
27300  PRINT "YOUR MAX INTELLIGENCE IS HALVED"
27320 PIM=PIM/2: IF PI>PIM THEN PI=PIM: GOTO 26780
27350  PRINT "ITS A ZAP WAND": GOTO 26780
27500  IF PD<0 THEN 21015: PRINT "HELP! HELP! HELP!"
27501 G=GOLD-10*(PS-PA*2): IF G<=0 OR GOLD<=0 THEN 27510: PRINT "YOU HAD TO DROP SOME GOLD": GOSUB 27502: GOTO 27510
27502  IF G=0 THEN  RETURN : IF G>GOLD THEN G=GOLD: FOR J=1 TO NR: IF CHL(J)<0 THEN 27504: IF CHC(J)<0 THEN 27504
27503 CHC(J)=CHC(J)+G:EXP=EXP-G:GOLD=GOLD-G:G=0
27504  NEXT J: RETURN 
27510 K=10:PD=PD/2
27520 CLR=10:X=PL MOD H:Y=PL/H: GOSUB 4000: IF J#0 THEN 27535: PRINT "THERE IS NOWHERE TO RUN!": GOTO 18000
27535 K=K-1: IF X+DX<0 OR X+DX>=H OR Y+DY<0 OR Y+DY>=V THEN 27595
27540  IF K=0 THEN 27595:L=PL+DX+DY*H
27550  IF MAP(L)#10 THEN 27595
27560 C=MAP(PL):PX=X:PY=Y: CALL 2051
27570 PL=L:X=PL MOD H:Y=PL/H:C=2
27580 PX=X:PY=Y: CALL 2051
27590  GOTO 27535
27595 UPM=1:UPN=1:ALR=1: GOSUB 10000: GOTO 18000
28000 NCL=INP-176
28005  IF NCL=0 THEN NCL=32767
28010 SC=0
28020  IF PS=PSM THEN 28100
28040 PS=PS+(PF/20): IF PS>PSM THEN PS=PSM:NCL=NCL-1: GOSUB 10000: IF NCL=0 THEN 28320
28100  IF PI=PIM THEN 28200
28120 PI=PI+(PF/20): IF PI>PIM THEN PI=PIM:NCL=NCL-1: GOSUB 10000: IF NCL=0 THEN 28320
28200  IF PD=PDM THEN 28300
28220 PD=PD+(PF/20): IF PD>PDM THEN PD=PDM:NCL=NCL-1: GOSUB 10000: IF NCL=0 THEN 28320
28300  IF PS#PSM OR PI#PIM OR PD#PDM THEN 28020
28320 UPN=1: IF INP= ASC("0") THEN 18000: IF NCL<=0 THEN 18000: FOR N=1 TO NCL: GOSUB 10000: NEXT N: GOTO 18000
29000  IF PI<PF/10 THEN 30020
29010  IF PB<PBM THEN 29020
29015  PRINT "YOU ARE ALREADY HEALED": GOTO 18000
29020 PI=PI-(PF/10):NCL=1
29040 PB=PB+(PF/10): IF PB>PBM THEN PB=PBM:UPN=1: GOSUB 10000: GOTO 18000
29500  IF PI<(PF/4)+5 THEN 30020
29520 PI=PI-(PF/4)-5
29530 G=GOLD: GOSUB 27502
29540 C=MAP(PL):PX=PL MOD H:PY=PL/H: CALL 2051
29560 PL= RND (H*V)
29580  IF PL>=H*V THEN PL=0
29600  IF MAP(PL)=10 THEN 29640
29620 PL=PL+1: GOTO 29580
29640 ALR=1: PRINT "POOF!"
29660 UPM=1:UPN=1: GOSUB 10000: GOTO 18000
30000  IF PI>=(PF/4)+15 THEN 30040
30020  PRINT "YOU'RE INTELLIGENCE IS TOO LOW": GOTO 18060
30030  GOSUB 10000: GOSUB 10000: GOSUB 10000
30040 PI=PI-(PF/4)-15:UPN=1
30060 RD=3: GOSUB 3010: PRINT "XRAY COMPLETE": GOTO 18000
31000  IF PI<PF/2 AND MI(7)=0 THEN 30020
31020  GOTO 23000
31500 G=GOLD-20*(PS-PA*2): IF G<=0 THEN 18000: GOSUB 27502:UPN=1: GOTO 18000
32500  IF  RND (2) THEN 32600: PRINT "BOOM! IT EXPLODES!"
32505  GOSUB 6000
32510  PRINT "MUST NOT BE THE ONE YOU'RE LOOKING FOR"
32520 PB=0: GOSUB 5040: GOTO 26780
32600  TEXT : CALL -936: PRINT "CONGRATULATIONS!"
32620  PRINT "YOU'VE FOUND THE MOST MAGICAL DEVICE OF"
32660  PRINT "THEM ALL!  HIT RETURN TO SEE WHAT IT CAN DO...": INPUT A$: END 

